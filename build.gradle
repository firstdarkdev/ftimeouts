subprojects {
    ext {
        release=project.properties['release'] ?: false
    }

    def version_base = "${project.version_major}.${project.version_minor}"
    version = "${version_base}.${project.version_patch}"

    // Jenkins
    if (!release && System.getenv('BUILD_NUMBER') != null) {
        version = version_base + "." + System.getenv('BUILD_NUMBER') + "d"
    }

    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    //java.withSourcesJar()
    //java.withJavadocJar()

    jar {
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestampe'              : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'First Dark Dev Maven Snapshots'
            url = 'https://maven.firstdarkdev.xyz/snapshots'
        }
        maven {
            name = 'First Dark Dev Maven Releases'
            url = 'https://maven.firstdarkdev.xyz/releases'
        }
        maven {
            name = "TerraformersMC Maven"
            url = "https://maven.terraformersmc.com/releases"
        }
        maven {
            name = "Curseforge Maven"
            url 'https://cfa2.cursemaven.com'
        }

        maven { url = "https://maven.k-4u.nl" }
        maven { url = "https://maven.wispforest.io/" }
        maven { url "https://maven.bai.lol" }
    }


    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    clean {
        delete "$rootDir/artifacts"
    }
}
